plugins {
    id 'com.github.hierynomus.license'      version '0.16.1' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    //apply plugin: 'test-report-aggregation'

    version = secp256k1Version     // set in gradle.properties
    group = 'org.bitcoinj.secp256k1'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.0"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.10.0"
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.0"
    }

    java {
        withJavadocJar()
        withSourcesJar()
        sourceCompatibility = JavaVersion.toVersion("22")
        targetCompatibility = JavaVersion.toVersion("22")
        toolchain {
            // `languageVersion` is used to configure the "Java Toolchain" used for the build. This includes `javac`,
            // `jlink`, and the `jpackage` tool.
            //  See `gradle.properties` for the setting of `javaToolchainVersion` and other setting that are used
            // to find and/or download JDK versions.
            languageVersion = JavaLanguageVersion.of(javaToolchainVersion)
            vendor = JvmVendorSpec.matching(javaToolchainVendor)
        }
    }

    test {
        useJUnitPlatform()
        def userHome = System.getProperty("user.home")
        systemProperty "java.library.path", findProperty("javaPath") ?: "${userHome}/.nix-profile/lib"
    }

    tasks.withType(JavaCompile).configureEach {
        options.release = 22
    }
}

apply from: 'gradle/licenseCheck.gradle'
apply from: 'gradle/maven-publish.gradle'

