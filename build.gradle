plugins {
    id 'com.github.hierynomus.license'      version '0.16.1' apply false
    id 'io.freefair.aggregate-javadoc'      version '9.0.0'
}

// Projects to be published with maven-publish
ext.publishedProjects = ['secp-api', 'secp-ffm', 'secp-bouncy', 'secp-graalvm']

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects { sub ->
    apply plugin: 'java'
    //apply plugin: 'test-report-aggregation'

    version = secpVersion     // set in gradle.properties
    group = 'org.bitcoinj.secp'

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.14.1"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.14.1"
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.14.1"
    }

    java {
        if (sub.name in publishedProjects) {
            withJavadocJar()
        }
        withSourcesJar()
        toolchain {
            // `languageVersion` is used to configure the "Java Toolchain" used for the build. This includes `javac`,
            // `jlink`, and the `jpackage` tool.
            //  See `gradle.properties` for the setting of `javaToolchainVersion` and other setting that are used
            // to find and/or download JDK versions.
            languageVersion = JavaLanguageVersion.of(javaToolchainVersion)
            vendor = JvmVendorSpec.matching(javaToolchainVendor)
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile).configureEach {
        // Default release version, secp-api and secp-bouncy override this to set an earlier release.
        options.release = 24
    }
}

// Aggregated Javadoc depends upon subprojects in publishedProjects
dependencies {
    subprojects {
        if (it.name in publishedProjects) {
            javadoc it
        }
    }
}

apply from: 'gradle/licenseCheck.gradle'
apply from: 'gradle/maven-publish.gradle'
