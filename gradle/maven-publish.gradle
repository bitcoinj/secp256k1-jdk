subprojects { sub ->

    if (sub.name in publishedProjects) {
        apply plugin: 'maven-publish'

        publishing {
            publications {
                jar(MavenPublication) {
                    from components.java
                    if (sub.name in publishedJava8Projects) {
                        tasks.register('java8Jar', Jar) {
                            dependsOn tasks.jar
                            archiveBaseName = "${sub.name}-java8"
                            from zipTree(tasks.jar.archiveFile)
                            exclude 'module-info.class'
                        }
                        artifacts {
                            archives tasks.java8Jar
                        }
                    }
                }
            }
            repositories {
                def secpJdkGitLabProjectId = "55956336"
                maven {
                    url = "https://gitlab.com/api/v4/projects/${secpJdkGitLabProjectId}/packages/maven"
                    name = "GitLab"
                    credentials(HttpHeaderCredentials) {
                        name = 'Private-Token'
                        value = project.findProperty("bitcoinjSecpPublishToken")
                    }
                    authentication {
                        header(HttpHeaderAuthentication)
                    }
                }
            }
        }
    }
}
