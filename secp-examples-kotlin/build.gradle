plugins {
    id "org.jetbrains.kotlin.jvm" version "2.0.20-RC2"
    id 'application'
}

dependencies {
    implementation project(':secp-api')
    runtimeOnly project(':secp-bouncy')
    runtimeOnly project(':secp-ffm')
}

tasks.withType(JavaCompile).configureEach {
    options.release = 22
}

kotlin {
    jvmToolchain(22)
}

application {
    mainClass = 'org.bitcoinj.secp.kotlin.examples.Schnorr'
}

def userHome = System.getProperty("user.home")
def javaLibraryPath = findProperty("javaPath") ?: "${userHome}/.nix-profile/lib"

run {
    systemProperty "java.library.path", javaLibraryPath
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

tasks.register('runEcdsa', JavaExec) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(22)
    }
    systemProperty "java.library.path", javaLibraryPath
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.bitcoinj.secp.kotlin.examples.Ecdsa'
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}
