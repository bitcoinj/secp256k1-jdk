import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id "org.jetbrains.kotlin.jvm" version "2.3.0-RC3"
    id 'application'
}

dependencies {
    implementation project(':secp-api')
    runtimeOnly project(':secp-bouncy')
    runtimeOnly project(':secp-ffm')
}

tasks.withType(JavaCompile).configureEach {
    // Override Default release version
    options.release = 25
}

kotlin {
    jvmToolchain(javaToolchainVersion as int)
    compilerOptions.jvmTarget = JvmTarget.JVM_25
}

application {
    mainClass = 'org.bitcoinj.secp.kotlin.examples.SchnorrKt'
}

run {
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}

tasks.register('runEcdsa', JavaExec) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(javaToolchainVersion)
    }
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.bitcoinj.secp.kotlin.examples.EcdsaKt'
    jvmArgs += '--enable-native-access=ALL-UNNAMED'
}
