plugins {
    id 'java-library'
    id 'org.beryx.jar' version '2.0.0'
}

tasks.withType(JavaCompile).configureEach {
    // IDEs and other tools will see `9` by default, but CI and release builds
    // will set `-PapiModuleJavaCompatibility=8` to generate a Java 8 JAR
    // and the `org.beryx.jar` plugin will put `module-info.jar` in the Java 8 JAR.
    options.release = (findProperty('apiModuleJavaCompatibility') ?: 9) as int
}

ext.moduleName = 'org.bitcoinj.secp.bouncy'

moduleConfig {
    version = project.version
}

dependencies {
    api project(':secp-api')
    api 'org.bouncycastle:bcprov-jdk18on:1.78.1'  // TODO: Make this a non-API dependency, see BouncyPrivKey
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes  'Implementation-Title': 'Secp256k1 Bouncy Castle Implementation',
                'Implementation-Version': archiveVersion.get()
    }
}

tasks.register('java8Jar', Jar) {
    dependsOn tasks.jar                 // Depend on the original Java 9 JAR
    archiveBaseName = "${project.name}-java8"
    from zipTree(tasks.jar.archiveFile) // Unpack the original JAR
    exclude 'module-info.class'         // Remove the module-info.class file

    // Copy the original manifest
    manifest {
        from tasks.jar.manifest
    }
}
